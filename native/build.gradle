import org.gradle.internal.jvm.Jvm

plugins {
    id "java"
    id "c"
}

model {
    platforms {
        x64 {
            architecture "x64"
        }
    }

    components {
        zachtronicsjni(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDirs "src/main/c", "src/jni/c"
                        include "**/*.c"
                        exclude "**/run-tests.c", "**/main.c"
                    }
                    exportedHeaders {
                        srcDirs "src/main/c", "src/jni/c"
                    }
                }
            }
            binaries.all {
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${Jvm.current().javaHome}/include"
                    cCompiler.args "-I${Jvm.current().javaHome}/include/win32"
                    linker.args "Shlwapi.lib", "Advapi32.lib"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${Jvm.current().javaHome}/include/freebsd"
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework:spring-core:5.3.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

processResources {
    dependsOn "zachtronicsjniSharedLibrary"
    from("$buildDir/libs/zachtronicsjni/") {
        include 'shared/*.so'
    }
}

test {
    useJUnitPlatform()
}

test.dependsOn "zachtronicsjniSharedLibrary"

